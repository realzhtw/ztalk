(macro (record name &rest fields)
  (with (nm (symbol-name name))
    `(do
       (def (,(symbol (append "make-" nm)) ,@fields)
         (annotate 'record
                   (vector (quote ,name) ,@fields)))
       (def (,(symbol (append nm "?")) x)
         (and (record? x)
              (is (vector-ref (rep x) 0) (quote ,name))))
       ,@(accumulate acc
           (w/uniq (g)
             (for x (enumerate fields 1)
               (with (i (car x)
                      f (cadr x))
                 (acc `(def (,(symbol (append nm "-" (symbol-name f))) ,g)
                         (vector-ref (rep ,g) ,i))))))))))

(def (record? x) (is-a x 'record))

(def-method (write-object (x record) s)
  (w/stdout s
    (let r (rep x)
      (print "<record:" (ref r 0) " [")
      (w/padding p #\space
        (for i (range 1 (length r))
          (print p (ref r i))))
      (print "]>"))))
